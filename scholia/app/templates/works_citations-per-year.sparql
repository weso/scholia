#defaultView:LineChart
SELECT ?year ?number_of_citations ?work 
WITH {
  SELECT ?year (COUNT(DISTINCT ?citing_work) AS ?number_of_citations) ?work1 WHERE {
    VALUES ?work1 {   {% for q in qs %} wd:{{ q }} {% endfor %}   }
    ?citing_work wdt:P2860 ?work1 .

    # Year of citation 
    ?citing_work wdt:P577 ?date .
    BIND(STR(YEAR(?date)) AS ?year)
  }
  GROUP BY ?year ?work1
} AS %counts
WITH {
  SELECT ?year ?number_of_citations ?work1 (SAMPLE(?work1Labels) AS ?work1Label) WHERE {
    INCLUDE %counts
    ?work1 rdfs:label ?work1Labels .
  }
  GROUP BY ?year ?number_of_citations ?work1
} AS %results
WHERE {
  INCLUDE %results
  BIND(CONCAT(?work1Label, " (", SUBSTR(STR(?work1), 32), ")") AS ?work) 
}
